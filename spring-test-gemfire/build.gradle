plugins {
  id 'java-library'
  id "io.freefair.lombok" version "8.0.1"
  id 'dependency-constraints'
	id 'gemfire-repo-artifact-publishing'
  id 'com.netflix.nebula.optional-base' version '8.0.0' // if you want optional-base
}


description = "Spring Test Framework for VMware GemFire 10.0-beta and Spring Data 3.0"

project.ext.set("pomProjectLongName", description)
project.ext.set("pomProjectArtifactName", "spring-test-gemfire-${getGemFireBaseVersion()}-3.0")
project.ext.set("pomProjectDescription", description)

java {
  withJavadocJar()
  withSourcesJar()
}

javadoc{
  failOnError=false
}

dependencies {

  api "edu.umd.cs.mtc:multithreadedtc:1.01"
  api "junit:junit"
  api "org.assertj:assertj-core"
  api "org.mockito:mockito-core"
  api "org.projectlombok:lombok"
  api "org.springframework:spring-test:6.0.7"
  compileOnly("com.vmware.gemfire:spring-data-3.0-gemfire-${getGemFireBaseVersion()}:$springDataGemFireVersion")

  implementation 'jakarta.annotation:jakarta.annotation-api:2.1.1'


  compileOnly "com.google.code.findbugs:jsr305:$findbugsVersion"

  implementation "ch.qos.logback:logback-classic", optional

  compileOnly('org.springframework.boot:spring-boot-starter') {
    exclude group: "org.springframework.boot", module: "spring-boot-starter-logging";
  }

  compileOnly "com.vmware.gemfire:gemfire-server-all:$gemfireVersion"

  testImplementation ("com.vmware.gemfire:gemfire-server-all:$gemfireVersion"){
    exclude group: 'com.vmware.gemfire', module: 'gemfire-log4j'
  }

  testImplementation("com.vmware.gemfire:spring-data-3.0-gemfire-${getGemFireBaseVersion()}:$springDataGemFireVersion") {
    exclude group: 'com.vmware.gemfire'
  }
  testImplementation "com.vmware.gemfire:gemfire-core:$gemfireVersion"
  testImplementation "com.vmware.gemfire:gemfire-logging:$gemfireVersion"
  testImplementation "com.vmware.gemfire:gemfire-cq:$gemfireVersion"
  testImplementation "com.vmware.gemfire:gemfire-wan:$gemfireVersion"
  testImplementation "com.vmware.gemfire:gemfire-gfsh:$gemfireVersion"
  testImplementation "com.vmware.gemfire:gemfire-search:1.0.0"
  testImplementation "com.vmware.gemfire:gemfire-tcp-server:$gemfireVersion"
  testImplementation "com.vmware.gemfire:gemfire-deployment-chained-classloader:$gemfireVersion"
  testImplementation "com.vmware.gemfire:gemfire-version:$gemfireVersion"
}

repositories {
  def additionalMavenRepoURLs = project.ext.get('additionalMavenRepoURLs')
  if (!additionalMavenRepoURLs.isEmpty() && !additionalMavenRepoURLs.isBlank()) {
    additionalMavenRepoURLs.split(',').each {
      project.getRepositories()
              .maven(mavenRepository -> {
                mavenRepository.setUrl(uri(it.toString()))
              })
    }
  }
}

String getGemFireBaseVersion() {
  def split = "${gemfireVersion}".split("\\.")
  if (split.length < 2) {
    throw new RuntimeException("gemfireVersion is malformed")
  }
  return "${split[0]}.${split[1]}"
}
